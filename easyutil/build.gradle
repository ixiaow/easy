apply plugin: 'com.android.library'
//jcenter
apply plugin: 'com.novoda.bintray-release'//添加
// jitpack
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.xiaowujiang'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.2.0'
    testCompile 'junit:junit:4.12'
}

/*************************************************** JitPack 配置***********************************/
// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
/***********************************************Jcenter配置*****************************************/
publish {
    userOrg = 'xiaowujiang'//bintray.com用户名
    groupId = 'cn.xwj'//jcenter上的路径
    artifactId = 'easyutils'//项目名称
    publishVersion = '1.0.3'//版本号
    desc = 'easyutil'//描述，不重要
    website = 'https://github.com/xiaowujiang/EasyUtils'//网站，不重要；尽量模拟github上的地址

}